plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'org.jetbrains.kotlin.android'
    id "com.google.protobuf" version "0.8.17"
    id "dagger.hilt.android.plugin"
    id "kotlin-kapt"
}

android {
    compileSdk TARGET_SDK as int

    defaultConfig {
        applicationId "com.jmat.powertools"
        minSdk MIN_SDK as int
        targetSdk TARGET_SDK as int
        versionCode APP_VERSION_CODE as int
        versionName APP_VERSION_NAME

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JDK_VERSION
        targetCompatibility JDK_VERSION
    }

    kotlinOptions {
        jvmTarget = JDK_VERSION
    }

    buildFeatures {
        viewBinding true
    }

    dynamicFeatures = [':app:dashboard', ':app:conversions']
}

dependencies {
    api "androidx.core:core-ktx:${KTX_CORE_VERSION}"
    api "androidx.appcompat:appcompat:${ANDROID_SUPPORT_PACKAGE_VERSION}"
    api "com.google.android.material:material:${MATERIAL_VERSION}"
    api "androidx.constraintlayout:constraintlayout:${CONSTRAINT_LAYOUT_VERSION}"
    api "androidx.navigation:navigation-fragment-ktx:${NAVIGATION_VERSION}"
    api "androidx.navigation:navigation-ui-ktx:${NAVIGATION_VERSION}"
    api "androidx.lifecycle:lifecycle-runtime-ktx:${LIFECYCLE_VERSION}"

    api "androidx.datastore:datastore:${DATASTORE_VERSION}"
    api "androidx.datastore:datastore-core:${DATASTORE_VERSION}"
    api "com.google.protobuf:protobuf-javalite:${PROTOBUF_VERSION}"

    implementation "com.google.dagger:hilt-android:${HILT_VERSION}"
    kapt "com.google.dagger:hilt-compiler:${HILT_VERSION}"

    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
}

protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc:3.14.0"
    }

    // Generates the java Protobuf-lite code for the Protobufs in this project. See
    // https://github.com/google/protobuf-gradle-plugin#customizing-protobuf-compilation
    // for more information.
    generateProtoTasks {
        all().each { task ->
            task.builtins {
                java {
                    option 'lite'
                }
            }
        }
    }
}

kapt {
    correctErrorTypes = true
}